"""Build setup for chrome-linux-sysroot"""

module(name = "chrome-linux-sysroot", version = "0.0.3")

_BUILD_FILE = """
filegroup(
  name = "sysroot",
  srcs = glob(["*/**"], exclude = [
    "lib/systemd/system/system-systemd*cryptsetup.slice"
  ]),
  visibility = ["//visibility:public"],
)
"""

# Based on https://github.com/bazelbuild/rules_rust/tree/f21e47995a0297cc522d52449a1b0e774b414bc5/examples/cross_compile
SYSROOTS = {
    "arm64": "d303cf3faf7804c9dd24c9b6b167d0345d41d7fe4bfb7d34add3ab342f6a236c",
    "amd64": "5df5be9357b425cdd70d92d4697d07e7d55d7a923f037c22dc80a78e85842d2c",
    "mips64el": "ee94d723b36d1e643820fe7ee2a8f45b3664b4c5d3c3379ebab39e474a2c9f86",
    "mipsel": "cc3202718a58541488e79b0333ce936a32227e07228f6b3c122d99ee45f83270",
}

http_archive = use_repo_rule("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")
[http_archive(
    name = "debian_bullseye_{}".format(arch),
    build_file_content = _BUILD_FILE,
    sha256 = sha256,
    type = "tar.xz",
    urls = ["https://commondatastorage.googleapis.com/chrome-linux-sysroot/{}".format(sha256)],
) for arch, sha256 in SYSROOTS.items()]
